' VART
' ====
' An open-source tool for drawing 640x400 vector art on the mega65

#output "vart"

' menu system
' +------+--------+-----+------+----------+---------+------+------+------+-----+
' ! line ! circle ! box ! poly ! (x) fill ! palette ! load ! save ! undo ! redo!
' +------+--------+-----+------+----------+---------+------+------+------+-----+
' x = exit
' ? = show/hide menu
' ctrl+1-8 = select from 1st 8 colours
' mega+1-8 = select from 2nd 8 colours
' ctrl+d = choose from default palette
' ctrl+a = choose from alternate palette

.declares
'--------
#declare key$
#declare k,a$
#declare x%, y%, w%, h%, x2%, y2%
#declare mx%, my%, mb%, mb_old%
#declare menu_selection = -1
#declare ret%

#declare line_state%, lx0%, ly0%
#declare circle_state%, cx0%, cy0%, radius%
#declare box_state%, bx0%, by0%

#struct BUTTON x%, y%, w%, h%, type%, state%, text$

#define TYPE_NORMAL = 0
#define TYPE_TOGGLE = 1

#define MAX_BTN = 5

#define LINE   = 0
#define CIRCLE = 1
#define BOX    = 2
#define FILL   = 3
#define EXIT   = 4

BUTTON btns(MAX_BTN) = [     {x5F}
  [ 0,  0, 6, 3, TYPE_NORMAL, 0, "line" ],   {x5F}
  [ 6,  0, 8, 3, TYPE_NORMAL, 0, "circle" ], {x5F}
  [ 14, 0, 5, 3, TYPE_NORMAL, 0, "box" ],    {x5F}
  [ 19, 0, 6, 3, TYPE_TOGGLE, 0, "fill" ],   {x5F}
  [ 25, 0, 6, 3, TYPE_NORMAL, 0, "exit" ]    {x5F}
] 

.main
'----
  gosub init   
  gosub draw_menu_system
  gosub check_input
  gosub ender
  end

.init
'----
  screen 0,640,400,4
  for k = 0 to 62
    read x%
    poke $0600+k, x%
  next k
  mouse on, 1
  sprite 0,1,1
  return

#define CHR_HEIGHT = 2
#define CHR_WIDTH = 1

#define CHR_DIR_UP = 1
#define CHR_DIR_RIGHT = 2
#define CHR_DIR_DOWN = 4
#define CHR_DIR_LEFT = 8

.clear_menu
'----------
  for k=0 to 7
    edma 3, 640*2, 0, $40000+k*$4000
  next k
  return

.draw_menu_system
'----------------
  gosub clear_menu
  pen 0,1

  ' draw items
  for k = 0 to MAX_BTN-1
    x%=btns_x%(k)
    y%=btns_y%(k)
    w%=(x% + btns_w%(k))
    h%=(y% + btns_h%(k))

    pen 0, 1
    if menu_selection=k and btns_type%(k) <> TYPE_TOGGLE then begin
      box x%*8, y%*8, w%*8, h%*8, 1
      pen 0, 0
    bend:else begin
      box x%*8, y%*8, w%*8, h%*8
      if btns_type%(k) = TYPE_TOGGLE and btns_state%(k)<>0 then begin
        pen 0,3
        box x%*8, y%*8, w%*8, h%*8, 1
        pen 0,0
      bend
    bend
    char x%+1, y%*8+4, CHR_HEIGHT, CHR_WIDTH, CHR_DIR_RIGHT, btns_text$(k)
    pen 0, 1
  next k
  return

.on_click_line
'-------------
  if line_state% = 0 then lx0%=mx%:ly0%=my%:line_state%=1:dot mx%,my%:return
  if line_state% = 1 then begin
    line lx0%, ly0%, mx%, my%
    line_state% = 0
  bend
  return

.on_click_circle
'---------------
  if circle_state% = 0 then cx0%=mx%:cy0%=my%:circle_state%=1:return
  if circle_state% = 1 then begin
    radius% = sqr( (mx%-cx0%)^2 + (my%-cy0%)^2 )
    circle cx0%, cy0%, radius%, btns_state%(FILL)
    circle_state% = 0
  bend
  return

.on_click_box
'------------
  if box_state% = 0 then bx0%=mx%:by0%=my%:box_state%=1:return
  if box_state% = 1 then begin
    box bx0%, by0%, mx%, my%, btns_state%(FILL)
    box_state% = 0
  bend
  return

.on_click_fill
'-------------
  btns_state%(FILL) = abs(not btns_state%(FILL))
  gosub draw_menu_system
  return

.check_action_click
'------------------
  if menu_selection = LINE then gosub on_click_line
  if menu_selection = CIRCLE then gosub on_click_circle
  if menu_selection = BOX then gosub on_click_box
  return

.check_colour_keypress
'---------------------
  if a$=chr$(144) then pen 0,0:sprite 0,1,0
  if a$=chr$(5) then pen 0,1:sprite 0,1,1
  if a$=chr$(28) then pen 0,2:sprite 0,1,2
  if a$=chr$(159) then pen 0,3:sprite 0,1,3
  if a$=chr$(156) then pen 0,4:sprite 0,1,4
  if a$=chr$(30) then pen 0,5:sprite 0,1,5
  if a$=chr$(31) then pen 0,6:sprite 0,1,6
  if a$=chr$(158) then pen 0,7:sprite 0,1,7
  if a$=chr$(129) then pen 0,8:sprite 0,1,8
  if a$=chr$(149) then pen 0,9:sprite 0,1,9
  if a$=chr$(150) then pen 0,10:sprite 0,1,10
  if a$=chr$(151) then pen 0,11:sprite 0,1,11
  if a$=chr$(152) then pen 0,12:sprite 0,1,12
  if a$=chr$(153) then pen 0,13:sprite 0,1,13
  if a$=chr$(154) then pen 0,14:sprite 0,1,14
  if a$=chr$(155) then pen 0,15:sprite 0,1,15
  return

.check_input
'-----------
  get a$
  if a$="x" then return
  gosub check_colour_keypress

  rmouse mx%, my%, mb%
  mx%=(mx%-24)*2
  my%=(my%-50)*2

  if mb%=128 and mb_old%=0 then begin
    mb_old%=128

    gosub check_button_hit
    
    if ret%=1 then begin
      if menu_selection = EXIT then screen close:end
      if menu_selection = FILL then gosub on_click_fill
    bend:else begin
      gosub check_action_click
    bend
  bend

  if mb_old%=128 and mb%=0 then mb_old% = 0

  'debug stuff
  'a$ = str$(mx%) + ", " + str$(my%) + ", " + str$(mb%)
  'pen 0, 0
  'box 0, 64, 150, 80, 1
  'pen 0, 1
  'char 0, 64, CHR_HEIGHT, CHR_WIDTH, CHR_DIR_RIGHT, a$

  goto check_input

.check_button_hit
'----------------
  ret%=0
  for k = 0 to MAX_BTN-1
    x%=btns_x%(k)*8
    y%=btns_y%(k)*8
    w%=btns_w%(k)*8
    h%=btns_h%(k)*8
    x2%=x%+w%
    y2%=y%+h%
    if x%<=mx% and mx%<=x2% and y%<=my% and my%<=y2% then begin
      menu_selection = k
      ret%=1
      gosub draw_menu_system
      k=MAX_BTN-1
    bend
  next k
  return
  
.ender
'-----
  screen close
  mouse off
  return

.data
'mouse pointer
data %11111111, %00000000, %00000000
data %11111110, %00000000, %00000000
data %11111100, %00000000, %00000000
data %11111000, %00000000, %00000000
data %11111100, %00000000, %00000000
data %11101110, %00000000, %00000000
data %11000111, %00000000, %00000000
data %10000011, %10000000, %00000000

data %00000001, %11000000, %00000000
data %00000000, %11100000, %00000000
data %00000000, %01110000, %00000000
data %00000000, %00111000, %00000000
data %00000000, %00011100, %00000000
data %00000000, %00001110, %00000000
data %00000000, %00000111, %00000000
data %00000000, %00000011, %10000000

data %00000000, %00000001, %00000000
data %00000000, %00000000, %00000000
data %00000000, %00000000, %00000000
data %00000000, %00000000, %00000000
data %00000000, %00000000, %00000000
ÿ