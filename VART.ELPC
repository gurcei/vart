' VART
' ====
' An open-source tool for drawing 640x400 vector art on the mega65

#output "vart"

' #define DEBUG_SORT=1

' menu system
' +------+--------+-----+------+----------+---------+------+------+------+-----+
' ! line ! circle ! box ! poly ! (x) fill ! palette ! load ! save ! undo ! redo!
' +------+--------+-----+------+----------+---------+------+------+------+-----+
' x = exit
' ? = show/hide menu
' ctrl+1-8 = select from 1st 8 colours
' mega+1-8 = select from 2nd 8 colours
' ctrl+d = choose from default palette
' ctrl+a = choose from alternate palette

.declares
'--------
#declare key$
#declare k,m0%, m1%, a$
#declare x%, y%, w%, h%, x2%, y2%
#declare mx%, my%, mb%, mb_old%
#declare menu_selection = -1
#declare ret%
#declare last_colour% = -1
#declare selected_colour% = 1
#declare old_menu_selection% = -1
#declare ymin%, ymax%, idx%, pidx%, px%(20)
#declare rev%, y0%, y1%, x0%, x1%, tmp%, m, c, i%, j%, swapped%

#declare load_state% = -4
#declare fill_state%

#declare line_state%, lx0%, ly0%
#declare circle_state%, cx0%, cy0%, radius%
#declare ellipse_state%, ex0%, ey0%, xradius%, yradius%
#declare box_state%, bx0%, by0%
#declare poly_state%, poly_idx%=0, poly_x%(100), poly_y%(100)

#declare vec_data%(3000)
vec_data%(0) = asc("v")  ' HEADER FOR "VECTOR" DATA
vec_data%(1) = asc("e")
vec_data%(2) = asc("c")
#declare vec_size% = 3

#declare stor%(500), stor_cnt%

#struct BUTTON x%, y%, w%, h%, type%, state%, text$

#define TYPE_NORMAL = 0
#define TYPE_TOGGLE = 1

#define LINE    = 0
#define CIRCLE  = 1
#define ELLIPSE = 2
#define BOX     = 3
#define POLY    = 4
#define FILL    = 5
#define SAVE    = 6
#define LOAD    = 7
#define EXIT    = 8
#define MAX_BTN = 9

BUTTON btns(MAX_BTN) = [     {x5F}
  [ 0, 0, 0, 0, TYPE_NORMAL, 0, "line" ],    {x5F}
  [ 0, 0, 0, 0, TYPE_NORMAL, 0, "circle" ],  {x5F}
  [ 0, 0, 0, 0, TYPE_NORMAL, 0, "ellipse" ], {x5F}
  [ 0, 0, 0, 0, TYPE_NORMAL, 0, "box" ],     {x5F}
  [ 0, 0, 0, 0, TYPE_NORMAL, 0, "poly" ],    {x5F}
  [ 0, 0, 0, 0, TYPE_TOGGLE, 0, "fill" ],    {x5F}
  [ 0, 0, 0, 0, TYPE_NORMAL, 0, "save" ],    {x5F}
  [ 0, 0, 0, 0, TYPE_NORMAL, 0, "load" ],    {x5F}
  [ 0, 0, 0, 0, TYPE_NORMAL, 0, "exit" ]     {x5F}
] 

#define COLOUR = 63

.init_btns
'---------
btns_w%(0) = len(btns_text$(0))+2
btns_h%(0) = 3
for k = 1 to MAX_BTN-1
  btns_x%(k) = btns_x%(k-1) + btns_w%(k-1)
  btns_w%(k) = len(btns_text$(k)) + 2
  btns_h%(k) = 3
next k

.main
'----
  gosub init   
  gosub draw_menu_system
  gosub check_input
  gosub ender
  end

.init
'----
  screen 0,640,400,4
  for k = 0 to 62
    read x%
    poke $0600+k, x%
  next k
  mouse on, 1
  sprite 0,1,1
  return

#define CHR_HEIGHT = 2
#define CHR_WIDTH = 1

#define CHR_DIR_UP = 1
#define CHR_DIR_RIGHT = 2
#define CHR_DIR_DOWN = 4
#define CHR_DIR_LEFT = 8

.clear_menu
'----------
  for k=0 to 7
    edma 3, 640*2, 0, $40000+k*$4000
  next k
  return

.draw_menu_system
'----------------
  gosub clear_menu
  pen 0,1

  ' draw items
  for k = 0 to MAX_BTN-1
    x%=btns_x%(k)
    y%=btns_y%(k)
    w%=(x% + btns_w%(k))
    h%=(y% + btns_h%(k))

    pen 0, 1
    if menu_selection=k and btns_type%(k) <> TYPE_TOGGLE then begin
      box x%*8, y%*8, w%*8, h%*8, 1
      pen 0, 0
    bend:else begin
      box x%*8, y%*8, w%*8, h%*8
      if btns_type%(k) = TYPE_TOGGLE and btns_state%(k)<>0 then begin
        pen 0,3
        box x%*8, y%*8, w%*8, h%*8, 1
        pen 0,0
      bend
    bend
    char x%+1, y%*8+4, CHR_HEIGHT, CHR_WIDTH, CHR_DIR_RIGHT, btns_text$(k)
    pen 0, 1
  next k

  pen 0,selected_colour% : sprite 0,1,selected_colour%
  return

.store_colour
'------------
  if last_colour% <> selected_colour% then begin
    vec_data%(vec_size%) = COLOUR : vec_size% = vec_size% + 1
    vec_data%(vec_size%) = selected_colour% : vec_size% = vec_size% + 1
    last_colour% = selected_colour%
  bend
  return

.store
'-----
  for k = 0 to stor_cnt%
    vec_data%(vec_size%) = stor%(k)
    vec_size% = vec_size% + 1
  next k
  return

.store_line
'----------
  stor%(0) = LINE + 64 * btns_state%(FILL)
  stor%(1) = lx0% and 255 : stor%(2) = lx0% >> 8
  stor%(3) = ly0% and 255 : stor%(4) = ly0% >> 8
  stor%(5) = mx% and 255 : stor%(6) = mx% >> 8
  stor%(7) = my% and 255 : stor%(8) = my% >> 8
  stor_cnt% = 8 : gosub store
  return

.on_click_line
'-------------
  if line_state% = 0 then begin
    gosub store_colour
    lx0%=mx%
    ly0%=my%
    dot mx%,my%

    line_state%=1

  bend:else if line_state% = 1 then begin
    line lx0%, ly0%, mx%, my%

    gosub store_line

    line_state% = 0
  bend
  return

.store_circle
'------------
  stor%(0) = CIRCLE + 64 * btns_state%(FILL)
  stor%(1) = cx0% and 255 : stor%(2) = cx0% >> 8
  stor%(3) = cy0% and 255 : stor%(4) = cy0% >> 8
  stor%(5) = radius% and 255 : stor%(6) = radius% >> 8
  stor_cnt% = 6 : gosub store
  return

.on_click_circle
'---------------
  if circle_state% = 0 then begin
    gosub store_colour
    cx0%=mx%
    cy0%=my%

    circle_state%=1

  bend:else if circle_state% = 1 then begin
    radius% = sqr( (mx%-cx0%)^2 + (my%-cy0%)^2 )
    circle cx0%, cy0%, radius%, btns_state%(FILL)

    gosub store_circle

    circle_state% = 0
  bend
  return

.store_ellipse
'-------------
  stor%(0) = ELLIPSE + 64 * btns_state%(FILL)
  stor%(1) = ex0% and 255 : stor%(2) = ex0% >> 8
  stor%(3) = ey0% and 255 : stor%(4) = ey0% >> 8
  stor%(5) = xradius% and 255 : stor%(6) = xradius% >> 8
  stor%(7) = yradius% and 255 : stor%(8) = yradius% >> 8
  stor_cnt% = 8 : gosub store
  return

.on_click_ellipse
'----------------
  if ellipse_state% = 0 then begin
    gosub store_colour

    ex0%=mx%
    ey0%=my%

    ellipse_state%=1
  
  bend:else if ellipse_state% = 1 then begin
    xradius% = abs(mx%-ex0%)
    yradius% = abs(my%-ey0%)
    ellipse ex0%, ey0%, xradius%, yradius%, btns_state%(FILL)

    gosub store_ellipse

    ellipse_state% = 0
  bend
  return

.on_click_box
'------------
  if box_state% = 0 then begin
    gosub store_colour

    bx0%=mx%
    by0%=my%
    
    stor%(0) = BOX + 64 * btns_state%(FILL)
    stor%(1) = bx0% and 255 : stor%(2) = bx0% >> 8
    stor%(3) = by0% and 255 : stor%(4) = by0% >> 8

    box_state%=1

  bend: else if box_state% = 1 then begin
    box bx0%, by0%, mx%, my%, btns_state%(FILL)

    stor%(5) = mx% and 255 : stor%(6) = mx% >> 8
    stor%(7) = my% and 255 : stor%(8) = my% >> 8
    stor_cnt% = 8 : gosub store

    box_state% = 0
  bend
  return

.find_ymin_ymax
'--------------
  ymin% = poly_y%(0)
  ymax% = poly_y%(0)
  for idx% = 0 to poly_idx%-1
    if poly_y%(idx%) < ymin% then ymin% = poly_y%(idx%)
    if poly_y%(idx%) > ymax% then ymax% = poly_y%(idx%)
  next idx%
  return 

.is_y_in_line_yrange
'-------------------
  ret% = 0: rev% = 0
  y0% = poly_y%(idx%-1)
  y1% = poly_y%(idx%)
  if y0% > y1% then tmp%=y0%:y0% = y1%:y1% = tmp%:rev%=1
  if y0% <= y% and y% < y1% then ret% = 1
  return

.check_line_intersect
'--------------------
  gosub is_y_in_line_yrange
  if ret% = 0 then return

  x0% = poly_x%(idx%-1)
  x1% = poly_x%(idx%)
  if rev%=1 then tmp%=x0%:x0% = x1%:x1% = tmp%

  if x0% = x1% then x% =x0%:ret% = 1:return

  m = (y1%-y0%) / (x1% - x0%)
  c = y0% - m * x0%
  
  x% = (y% - c) / m
  ret% = 1
  return
  
.find_intersects
'---------------
  pidx% = 0

  ' check last-point to first
  poly_x%(poly_idx%) = poly_x%(0)
  poly_y%(poly_idx%) = poly_y%(0)

  for idx% = 1 to poly_idx%
    gosub check_line_intersect
    if ret% = 1 then px%(pidx%)=x% : pidx% = pidx% + 1
  next idx%

  return

.sort_intersects
'---------------
  ' bubble sort algorithm from: geeksforgeeks.org/bubble-sort
  
#ifdef DEBUG_SORT
  gosub clear_menu
  char 0,0,1,1,2,str$(pidx%-1)

  for i%=0 to pidx%-1
    char 4+i%*4,0,1,1,2,str$(px%(i%))
  next i%
#endif

  for i% = 0 to pidx%-2
    swapped% = 0
    for j% = 0 to pidx%-i%-2
      if px%(j%) > px%(j%+1) then begin
        tmp%=px%(j%)
        px%(j%) = px%(j%+1)
        px%(j%+1) = tmp%
        swapped% = 1
      bend
    next j%

    if swapped% = 0 then i% = pidx%  ' exit early
  next i%

#ifdef DEBUG_SORT
  for i%=0 to pidx%-1
    char 4+i%*4,10,1,1,2,str$(px%(i%))
  next i%

  getkey a$
#endif

  return

.draw_horizontal_intersects
'--------------------------
  gosub find_intersects
  gosub sort_intersects
  for idx%=0 to pidx%-1 step 2
    line px%(idx%),y%,px%(idx%+1),y%
  next idx%
  return

.fill_poly
'---------
  gosub find_ymin_ymax
  for y%=ymin% to ymax%
    gosub draw_horizontal_intersects
  next y%
  return

.on_click_poly
'-------------
  if poly_state% = 0 then begin
    gosub store_colour

    stor%(0) = POLY + 64 * btns_state%(FILL)
    stor%(1) = 0 ' placeholder for number of points
    stor_cnt% = 1

    poly_state% = 1

    poly_idx% = 0
  bend
  
  if poly_idx% > 0 then begin
    if poly_x%(poly_idx%-1)=mx% and poly_y%(poly_idx%-1)=my% then begin
      line mx%,my%,poly_x%(0),poly_y%(0)

      stor%(1) = poly_idx%
      gosub store

      poly_state% = 0
      if btns_state%(FILL) = 1 then gosub fill_poly
      return
    bend
  bend

  poly_x%(poly_idx%) = mx%
  poly_y%(poly_idx%) = my%

  stor_cnt%=stor_cnt%+1 : stor%(stor_cnt%) = mx% and 255
  stor_cnt%=stor_cnt%+1 : stor%(stor_cnt%) = mx% >> 8
  stor_cnt%=stor_cnt%+1 : stor%(stor_cnt%) = my% and 255
  stor_cnt%=stor_cnt%+1 : stor%(stor_cnt%) = my% >> 8

  if poly_idx% = 0 then begin
    dot mx%, my%
  bend:else begin
    line poly_x%(poly_idx%-1), poly_y%(poly_idx%-1), mx%, my%
  bend

  poly_idx% = poly_idx% + 1
  return

.on_click_fill
'-------------
  if btns_state%(FILL) = 0 then btns_state%(FILL)=1:else btns_state%(FILL)=0
  return

.on_click_save
'-------------
  border 3
  ' for now, just save to a single hard-coded file
  ^^dopen #2,"@test.v",w,u8
  for k = 0 to vec_size%-1
    print #2, chr$(vec_data%(k));
  next k
  dclose #2
  return

.load_error
'----------
  border 2
  load_state% = -4
  return

.check_vec_header
'----------------
  ret% = 0
  if k<>asc("v") then return
  get#2, k:print k;
  if k<>asc("e") then return
  get#2, k:print k;
  if k<>asc("c") then return
  ret% = 1
  vec_size% = 3
  load_state%=-1
  return


.on_click_load
'-------------
  ' for now, just load from a single hard-coded file
  load_state% = -4
  ^^dopen #2,"test.v",r,u8
  do
    get#2, k:print "rd=";k;
    if st then exit

    if load_state% = -4 then begin
      print "check_vec ";
      gosub check_vec_header
      if ret% = 0 then gosub load_error:exit:else goto nxt_lp
    bend

    if load_state% = -1 then begin
      print "new_cmd ";
      fill_state% = (k and 64)/64
      btns_state%(FILL) = fill_state%
      k = k and 63
      load_state% = k
    bend

    if load_state% = COLOUR then begin
      print "colour ";
      get#2,k:print k;
      pen 0, k

      vec_data%(vec_size%) = COLOUR
      vec_size% = vec_size% +1
      vec_data%(vec_size%) = k
      vec_size% = vec_size% +1
      last_colour% = k
      selected_colour% = k

      load_state% = -1
      goto nxt_lp
    bend

    if load_state% = LINE then begin
      print "line ";
      get#2,m0% : m0% = m0% and 255 : print m0%;
      get#2,m1% : m1% = m1% and 255 : print m1%;
      lx0% = m0% + (m1% << 8)
      get#2,m0% : m0% = m0% and 255 : print m0%;
      get#2,m1% : m1% = m1% and 255 : print m1%;
      ly0% = m0% + (m1% << 8)
      get#2,m0% : m0% = m0% and 255 : print m0%;
      get#2,m1% : m1% = m1% and 255 : print m1%;
      mx% = m0% + (m1% << 8)
      get#2,m0% : m0% = m0% and 255 : print m0%;
      get#2,m1% : m1% = m1% and 255 : print m1%;
      my% = m0% + (m1% << 8)
      line lx0%, ly0%, mx%, my%

      gosub store_line

      load_state% = -1
      goto nxt_lp
    bend

    if load_state% = CIRCLE then begin
      print "circle ";
      get#2,m0% : m0% = m0% and 255 : print m0%;
      get#2,m1% : m1% = m1% and 255 : print m1%;
      cx0% = m0% + (m1% << 8)
      get#2,m0% : m0% = m0% and 255 : print m0%;
      get#2,m1% : m1% = m1% and 255 : print m1%;
      cy0% = m0% + (m1% << 8)
      get#2,m0% : m0% = m0% and 255 : print m0%;
      get#2,m1% : m1% = m1% and 255 : print m1%;
      radius% = m0% + (m1% << 8)

      circle cx0%, cy0%, radius%, btns_state%(FILL)

      gosub store_circle
      load_state% = -1
      goto nxt_lp
    bend

    if load_state% = ELLIPSE then begin
      print "ellipse ";
      get#2,m0% : m0% = m0% and 255 : print m0%;
      get#2,m1% : m1% = m1% and 255 : print m1%;
      ex0% = m0% + (m1% << 8)
      get#2,m0% : m0% = m0% and 255 : print m0%;
      get#2,m1% : m1% = m1% and 255 : print m1%;
      ey0% = m0% + (m1% << 8)
      get#2,m0% : m0% = m0% and 255 : print m0%;
      get#2,m1% : m1% = m1% and 255 : print m1%;
      xradius% = m0% + (m1% << 8)
      get#2,m0% : m0% = m0% and 255 : print m0%;
      get#2,m1% : m1% = m1% and 255 : print m1%;
      yradius% = m0% + (m1% << 8)

      ellipse ex0%, ey0%, xradius%, yradius%, btns_state%(FILL)

      gosub store_ellipse
      load_state% = -1
      goto nxt_lp

    bend

.nxt_lp
  loop
  dclose #2

  return

.check_action_click
'------------------
  if menu_selection = LINE then gosub on_click_line
  if menu_selection = CIRCLE then gosub on_click_circle
  if menu_selection = ELLIPSE then gosub on_click_ellipse
  if menu_selection = BOX then gosub on_click_box
  if menu_selection = POLY then gosub on_click_poly
  return

.check_colour_keypress
'---------------------
  if a$=chr$(144) then selected_colour% = 0
  if a$=chr$(5)   then selected_colour% = 1
  if a$=chr$(28)  then selected_colour% = 2
  if a$=chr$(159) then selected_colour% = 3
  if a$=chr$(156) then selected_colour% = 4
  if a$=chr$(30)  then selected_colour% = 5
  if a$=chr$(31)  then selected_colour% = 6
  if a$=chr$(158) then selected_colour% = 7
  if a$=chr$(129) then selected_colour% = 8
  if a$=chr$(149) then selected_colour% = 9
  if a$=chr$(150) then selected_colour% = 10
  if a$=chr$(151) then selected_colour% = 11
  if a$=chr$(152) then selected_colour% = 12
  if a$=chr$(153) then selected_colour% = 13
  if a$=chr$(154) then selected_colour% = 14
  if a$=chr$(155) then selected_colour% = 15

  pen 0,selected_colour% : sprite 0,1,selected_colour%
  return

.check_input
'-----------
  get a$
  if a$="x" then return
  gosub check_colour_keypress

  rmouse mx%, my%, mb%
  mx%=(mx%-24)*2
  my%=(my%-50)*2

  if mb%=128 and mb_old%=0 then begin
    mb_old%=128

    gosub check_button_hit
    
    if ret%=1 then begin
      if menu_selection = EXIT then screen close:end
    bend:else begin
      gosub check_action_click
    bend
  bend

  if mb_old%=128 and mb%=0 then mb_old% = 0

  'debug stuff
  'a$ = str$(mx%) + ", " + str$(my%) + ", " + str$(mb%)
  'pen 0, 0
  'box 0, 64, 150, 80, 1
  'pen 0, 1
  'char 0, 64, CHR_HEIGHT, CHR_WIDTH, CHR_DIR_RIGHT, a$

  goto check_input

.check_button_hit
'----------------
  ret%=0
  for k = 0 to MAX_BTN-1
    x%=btns_x%(k)*8
    y%=btns_y%(k)*8
    w%=btns_w%(k)*8
    h%=btns_h%(k)*8
    x2%=x%+w%
    y2%=y%+h%
    if x%<=mx% and mx%<=x2% and y%<=my% and my%<=y2% then begin
      old_menu_selection% = menu_selection
      menu_selection = k
      if menu_selection = FILL then begin
        gosub on_click_fill
        menu_selection = old_menu_selection%

      bend: else if menu_selection = SAVE then begin
        gosub on_click_save

      bend: else if menu_selection = LOAD then begin
        gosub on_click_load
      bend

      ret%=1
      gosub draw_menu_system
      k=MAX_BTN-1
    bend
  next k
  return
  
.ender
'-----
  screen close
  mouse off
  return

.data
'mouse pointer
data %11111111, %00000000, %00000000
data %11111110, %00000000, %00000000
data %11111100, %00000000, %00000000
data %11111000, %00000000, %00000000
data %11111100, %00000000, %00000000
data %11101110, %00000000, %00000000
data %11000111, %00000000, %00000000
data %10000011, %10000000, %00000000

data %00000001, %11000000, %00000000
data %00000000, %11100000, %00000000
data %00000000, %01110000, %00000000
data %00000000, %00111000, %00000000
data %00000000, %00011100, %00000000
data %00000000, %00001110, %00000000
data %00000000, %00000111, %00000000
data %00000000, %00000011, %10000000

data %00000000, %00000001, %00000000
data %00000000, %00000000, %00000000
data %00000000, %00000000, %00000000
data %00000000, %00000000, %00000000
data %00000000, %00000000, %00000000
ÿ